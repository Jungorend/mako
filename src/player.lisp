(in-package #:mako)

(ecs:define-component player)

(ecs:define-system move-player
    (:components-ro (player)
     :components-no (wait)
     :components-rw (position tile)
     :enable (not *paused*))
  (setf *player-turn* t)
  (let ((dx 0) (dy 0))
    (when (key-pressed? 'move-up) (decf dy 1))
    (when (key-pressed? 'move-down) (incf dy 1))
    (when (key-pressed? 'move-left) (decf dx 1))
    (when (key-pressed? 'move-right) (incf dx 1))
    (let ((target-x (+ tile-col dx))
          (target-y (+ tile-row dy)))
      (cond
        ((and (= dx 0) (= dy 0))
         nil)
        ((entity-at-tile? 'wall target-x target-y)
         nil)
        ((entity-at-tile? 'enemy target-x target-y)
         (ecs:delete-entity (first (entity-at-tile? 'enemy target-x target-y))))
        (t
         (assign-wait entity :remaining-time 10)
         (incf tile-row dy)
         (incf tile-col dx)
         (setf tile-hash (a*:encode-integer-coordinates tile-col tile-row)
               position-x (* tile-col +tile-size+)
               position-y (* tile-row +tile-size+))
         (setf *player-turn* nil))))))
